pragma ComponentBehavior: Bound
import QtQuick
import QtQuick.Shapes
import QtQuick.Controls
import OpenNOW

FocusScope {
    id: root
    objectName: "desktopLibraryScreen"
    property string searchQuery: ""
    property string activeFilter: "all"
    readonly property var collection: ShellStore.activeCollection
    readonly property var collectionGameIds: new Set(root.collection ? root.collection.gameIds : [])
    readonly property string selectedCollectionId: ShellStore.activeCollectionId
    onSelectedCollectionIdChanged: {
        activeFilter = "all"
        closeContext()
    }
    property var contextGame: null
    property var presentedContextGame: null
    onContextGameChanged: if (contextGame) presentedContextGame = contextGame
    MotionProgress { id: contextMotion; shown: root.contextGame !== null; enterDuration: 120; exitDuration: 120 }
    MotionProgress { id: collectionMotion; shown: root.contextGame !== null && root.collectionOpen; enterDuration: 120; exitDuration: 120 }
    property point contextPoint: Qt.point(0, 0)
    signal detailsRequested(var game)
    signal playRequested(var game)

    function storeBlob(game) {
        return (game.availableStores || []).join(" ").toLocaleLowerCase()
    }
    function hasRtx(game) {
        const blob = ((game.genres || []).join(" ") + " " + String(game.title || "") + " " + String(game.nvidiaTech || "")).toLocaleLowerCase()
        return blob.indexOf("rtx") >= 0 || blob.indexOf("ray tracing") >= 0
    }
    function hasController(game) {
        const controls = (game.supportedControls || []).join(" ").toLocaleLowerCase()
        return controls.indexOf("gamepad") >= 0 || controls.indexOf("controller") >= 0
    }
    function isReady(game) {
        return game.playabilityState === "PLAYABLE" && (game.variants || []).some(variant =>
            variant.gfnStatus === "AVAILABLE" && variant.playStatus !== "NOT_PLAYABLE" && !variant.stateDetails)
    }
    function favoriteLabel() {
        if (root.contextGame && ShellStore.isFavorite(root.contextGame))
            return qsTr("Remove from Home")
        return qsTr("Pin to Home")
    }
    function hideLabel() {
        if (root.contextGame && ShellStore.isHidden(root.contextGame))
            return qsTr("Unhide from library")
        return qsTr("Hide from library")
    }
    function countHidden() {
        return root.countWhere(root.isHiddenGame)
    }
    function isHiddenGame(game) {
        return ShellStore.isHidden(game)
    }
    function countStore(name) {
        return root.countWhere(function(game) { return root.storeBlob(game).indexOf(name) >= 0 })
    }
    function countWhere(predicate) {
        const source = ShellStore.catalogGames || []
        let count = 0
        for (let i = 0; i < source.length; ++i) {
            if (root.collection && !root.collectionGameIds.has(ShellStore.gameIdentity(source[i])))
                continue
            if (predicate(source[i]))
                count += 1
        }
        return count
    }
    function filteredGames() {
        const query = searchQuery.trim().toLocaleLowerCase()
        const source = activeFilter === "cloud-favorites" ? ShellStore.remoteFavorites : ShellStore.catalogGames || []
        const result = []
        for (let index = 0; index < source.length; ++index) {
            const game = source[index]
            if (root.collection && !root.collectionGameIds.has(ShellStore.gameIdentity(game)))
                continue
            if (query !== "" && String(game.title || "").toLocaleLowerCase().indexOf(query) < 0)
                continue
            const hidden = root.isHiddenGame(game)
            if (activeFilter === "hidden") {
                if (!hidden)
                    continue
            } else if (hidden) {
                continue
            }
            const stores = root.storeBlob(game)
            if (activeFilter === "ready" && !root.isReady(game))
                continue
            if (activeFilter === "rtx" && !root.hasRtx(game))
                continue
            if (activeFilter === "controller" && !root.hasController(game))
                continue
            if (activeFilter === "steam" && stores.indexOf("steam") < 0)
                continue
            if (activeFilter === "epic" && stores.indexOf("epic") < 0)
                continue
            if (activeFilter === "gog" && stores.indexOf("gog") < 0)
                continue
            result.push(game)
        }
        return result
    }
    readonly property var games: filteredGames()
    readonly property real tileScale: Math.max(0.75, Math.min(1.5, Number(ShellStore.settings.posterSizeScale || 1.05))) / 1.05
    readonly property int libraryColumns: Math.max(1, Math.floor((grid.width + 10) / (156 * tileScale)))
    readonly property int libraryCellW: Math.max(1, Math.floor(grid.width / libraryColumns))
    // The delegate's artwork keeps a 2:3 aspect inside a 6px gutter; derive the
    // cell from that geometry so the outline and overlay never clip at scale.
    readonly property int libraryCellH: Math.round((libraryCellW - 12) * 198 / 132) + 12

    function editCollection(mode, game) {
        collectionDialog.mode = mode
        collectionDialog.collectionId = mode === "create" ? "" : root.collection.id
        collectionDialog.initialName = mode === "create" ? "" : root.collection.name
        collectionDialog.game = game || null
        root.closeContext()
        collectionDialog.open()
    }

    DesktopCollectionDialog {
        id: collectionDialog
        onCollectionOpened: collectionId => ShellStore.activeCollectionId = collectionId
    }

    Row {
        id: collectionToolbar
        x: 24; y: 16
        width: parent.width - 48
        height: 36
        spacing: 8
        Text {
            width: Math.max(80, parent.width - collectionActions.width - parent.spacing)
            height: 36
            text: root.collection ? root.collection.name : qsTr("All games")
            textFormat: Text.PlainText
            elide: Text.ElideRight
            verticalAlignment: Text.AlignVCenter
            color: DesktopTokens.text
            font.family: DesktopTokens.displayFont
            font.pixelSize: DesktopTokens.titleSize
            font.weight: Font.Black
            font.letterSpacing: -0.6
        }
        Row {
            id: collectionActions
            spacing: 8
            DesktopButton {
                text: qsTr("All games")
                quiet: true
                visible: root.collection !== null
                onClicked: ShellStore.activeCollectionId = ""
            }
            DesktopButton {
                text: qsTr("Rename")
                quiet: true
                visible: root.collection !== null
                enabled: !ShellStore.collectionsBusy
                onClicked: root.editCollection("rename", null)
            }
            DesktopButton {
                text: qsTr("Delete")
                quiet: true
                visible: root.collection !== null
                enabled: !ShellStore.collectionsBusy
                onClicked: root.editCollection("delete", null)
            }
            DesktopButton {
                objectName: "libraryNewCollectionButton"
                text: qsTr("New collection")
                enabled: !ShellStore.collectionsBusy
                onClicked: root.editCollection("create", null)
            }
        }
    }

    Rectangle {
        id: catalogNotice
        objectName: "libraryCompletenessNotice"
        x: 24
        y: collectionToolbar.y + collectionToolbar.height + 14
        width: parent.width - 48
        height: visible ? Math.max(58, noticeText.implicitHeight + 24) : 0
        visible: root.activeFilter === "cloud-favorites" || (ShellStore.catalogSource === "account-library" && ShellStore.catalogState !== "ready")
        radius: 12
        color: DesktopTokens.raised
        Rectangle {
            x: 0; y: 10; width: 3; height: parent.height - 20; radius: 1.5
            color: ShellStore.catalogError !== "" ? DesktopTokens.danger : DesktopTokens.focus
        }
        Text {
            id: noticeText
            x: 18; y: 12; width: parent.width - noticeAction.width - 46
            text: root.activeFilter === "cloud-favorites" ? (ShellStore.remoteFavoritesError || qsTr("GeForce NOW favorites may show only part of your favorites. Refresh to check for updates. Home pins are separate.")) : ShellStore.catalogError || (ShellStore.catalogComplete
                ? qsTr("Refreshing the library. Your last complete library is still shown.")
                : qsTr("Loading your library. The games shown so far are only part of it."))
            wrapMode: Text.WordWrap
            color: DesktopTokens.textMuted
            font.family: DesktopTokens.bodyFont
            font.pixelSize: 13
        }
        DesktopButton {
            id: noticeAction
            anchors.right: parent.right; anchors.rightMargin: 12; anchors.verticalCenter: parent.verticalCenter
            text: root.activeFilter === "cloud-favorites" ? qsTr("Refresh") : ShellStore.catalogNextCursor ? qsTr("Continue") : qsTr("Retry")
            visible: root.activeFilter === "cloud-favorites" || (ShellStore.catalogRequestId === "" && ShellStore.catalogError !== "")
            enabled: root.activeFilter !== "cloud-favorites" || ShellStore.remoteFavoritesState !== "loading"
            onClicked: root.activeFilter === "cloud-favorites" ? ShellStore.refreshCloudFavorites() : ShellStore.continueCatalog()
        }
    }

    Flow {
        id: filterRow
        x: 24
        y: catalogNotice.y + catalogNotice.height + (catalogNotice.visible ? 12 : 0)
        width: parent.width - 48
        spacing: 8
        Repeater {
            model: [
                {key:"all", label:qsTr("All"), count:root.countWhere(function(game) { return !root.isHiddenGame(game) })},
                {key:"cloud-favorites", label:qsTr("GeForce NOW favorites"), count:ShellStore.remoteFavorites.length},
                {key:"ready", label:qsTr("Available versions"), count:root.countWhere(root.isReady)},
                {key:"rtx", label:"RTX", count:root.countWhere(root.hasRtx)},
                {key:"controller", label:qsTr("Controller"), count:root.countWhere(root.hasController)},
                {key:"steam", label:"Steam", count:root.countStore("steam")},
                {key:"epic", label:"Epic", count:root.countStore("epic")},
                {key:"gog", label:"GOG", count:root.countStore("gog")},
                {key:"hidden", label:qsTr("Hidden"), count:root.countHidden()}
            ]
            delegate: Button {
                id: filterButton
                required property var modelData
                visible: modelData.key !== "hidden" || root.countHidden() > 0
                height: 40
                implicitHeight: 40
                implicitWidth: Math.max(84, chipRow.implicitWidth + 28)
                padding: 0
                leftPadding: 0
                rightPadding: 0
                topPadding: 0
                bottomPadding: 0
                focusPolicy: Qt.StrongFocus
                hoverEnabled: true
                clip: false
                background: Rectangle {
                    radius: 12
                    color: root.activeFilter === filterButton.modelData.key
                        ? Qt.rgba(DesktopTokens.focus.r, DesktopTokens.focus.g, DesktopTokens.focus.b, 0.16)
                        : (filterButton.hovered || filterButton.activeFocus ? DesktopTokens.raised : "transparent")
                    border.width: root.activeFilter === filterButton.modelData.key ? 1 : 0
                    border.color: DesktopTokens.focus
                    Behavior on color { ColorAnimation { duration: DesktopTokens.quickDuration } }
                    Rectangle {
                        anchors.fill: parent; anchors.margins: -3; radius: parent.radius + 3
                        color: "transparent"; border.width: 2; border.color: DesktopTokens.focus
                        visible: filterButton.activeFocus && !(root.activeFilter === filterButton.modelData.key)
                    }
                }
                contentItem: Item {
                    implicitWidth: chipRow.implicitWidth
                    implicitHeight: 40
                    Row {
                        id: chipRow
                        anchors.centerIn: parent
                        spacing: 8
                        Image {
                            visible: ["steam","epic","gog"].indexOf(filterButton.modelData.key) >= 0
                            width: 16
                            height: 16
                            anchors.verticalCenter: parent.verticalCenter
                            source: visible ? DesktopTokens.storeIconUrl(filterButton.modelData.key) : ""
                            sourceSize: Qt.size(32, 32)
                            fillMode: Image.PreserveAspectFit
                        }
                        Text {
                            text: filterButton.modelData.label
                            anchors.verticalCenter: parent.verticalCenter
                            color: root.activeFilter === filterButton.modelData.key ? DesktopTokens.text : DesktopTokens.textMuted
                            font.family: DesktopTokens.bodyFont
                            font.pixelSize: 14
                            font.weight: root.activeFilter === filterButton.modelData.key ? Font.Bold : Font.DemiBold
                            verticalAlignment: Text.AlignVCenter
                        }
                        Text {
                            text: filterButton.modelData.count
                            anchors.verticalCenter: parent.verticalCenter
                            color: DesktopTokens.textFaint
                            font.family: DesktopTokens.monoFont
                            font.pixelSize: 12
                            font.weight: Font.DemiBold
                            verticalAlignment: Text.AlignVCenter
                        }
                    }
                }
                onClicked: root.activeFilter = modelData.key
            }
        }
    }
    Text {
        id: libraryHint
        anchors.right: parent.right
        anchors.rightMargin: 24
        anchors.verticalCenter: filterRow.verticalCenter
        visible: filterRow.height <= 40 && root.width - 24 - libraryHint.implicitWidth > 40 + filterRow.childrenRect.width
        text: qsTr("RIGHT-CLICK A GAME FOR ACTIONS")
        color: DesktopTokens.textFaint
        font.family: DesktopTokens.monoFont
        font.pixelSize: 10
        font.weight: Font.DemiBold
        font.letterSpacing: 0.7
    }

    GridView {
        id: grid
        // The delegate keeps a six-pixel focus/scale gutter. Offset the view by
        // that gutter so the artwork remains on Paper's 24/64 alignment lane.
        x: 18
        y: filterRow.y + filterRow.height + 14
        width: parent.width - 36
        height: parent.height - y
        clip: true
        cellWidth: root.libraryCellW
        cellHeight: root.libraryCellH
        model: root.games
        focus: true
        boundsBehavior: Flickable.StopAtBounds
        ScrollBar.vertical: ScrollBar { policy: ScrollBar.AsNeeded }
        delegate: DesktopPoster {
            required property var modelData
            refined: true
            game: modelData
            tileWidth: root.libraryCellW
            tileHeight: root.libraryCellH
            onClicked: {
                ShellStore.selectedGame = modelData
                root.detailsRequested(modelData)
            }
            onDoubleClicked: {
                ShellStore.selectedGame = modelData
                root.playRequested(modelData)
            }
            onContextRequested: (sceneX, sceneY) => {
                root.contextGame = modelData
                const local = root.mapFromItem(null, sceneX, sceneY)
                root.contextPoint = Qt.point(Math.min(root.width - 470, Math.max(16, local.x)), Math.min(root.height - 300, Math.max(16, local.y)))
                root.collectionOpen = false
            }
        }
    }

    Column {
        anchors.centerIn: grid
        width: Math.min(grid.width - 48, 460)
        spacing: 12
        visible: root.games.length === 0
        Text {
            width: parent.width
            text: root.collection ? qsTr("No games in this view") : qsTr("No games found")
            color: DesktopTokens.text
            font.family: DesktopTokens.displayFont
            font.pixelSize: DesktopTokens.titleSize
            font.weight: Font.Black
            font.letterSpacing: -0.4
            horizontalAlignment: Text.AlignHCenter
        }
        Text {
            width: parent.width
            text: root.collection
                ? qsTr("Open All games, right-click a game, and choose Add to collection. Games can belong to more than one collection.")
                : qsTr("Try a different search or filter.")
            color: DesktopTokens.textMuted
            font.family: DesktopTokens.bodyFont
            font.pixelSize: 14
            wrapMode: Text.WordWrap
            horizontalAlignment: Text.AlignHCenter
        }
    }

    MouseArea {
        anchors.fill: parent; z: 40
        visible: root.contextGame !== null
        acceptedButtons: Qt.LeftButton | Qt.RightButton
        onClicked: { root.contextGame = null; root.collectionOpen = false }
    }
    property bool collectionOpen: false
    function closeContext() {
        root.contextGame = null
        root.collectionOpen = false
    }
    function activateContext(action) {
        const game = root.contextGame
        if (action === "favorite") {
            ShellStore.toggleFavorite(game)
            return
        }
        if (action === "hide") {
            ShellStore.toggleHidden(game)
            root.closeContext()
            return
        }
        root.closeContext()
        if (action === "play") { ShellStore.selectedGame = game; root.playRequested(game) }
        else if (action === "details") { ShellStore.selectedGame = game; root.detailsRequested(game) }
        else if (action === "settings") AppController.navigate("settings-streaming")
    }
    Rectangle {
        x: root.contextPoint.x; y: root.contextPoint.y
        width: 230; height: 290; radius: 14
        visible: contextMotion.present
        enabled: root.contextGame !== null
        z: 41
        color: DesktopTokens.shell
        border.width: 1; border.color: DesktopTokens.seam
        Column {
            x: 8; y: 8; width: 214; spacing: 0
            Text { width: parent.width; height: 26; leftPadding: 8; text: root.presentedContextGame ? String(root.presentedContextGame.title || "").toUpperCase() : ""; color: DesktopTokens.textFaint; elide: Text.ElideRight; verticalAlignment: Text.AlignVCenter; font.family: DesktopTokens.monoFont; font.pixelSize: DesktopTokens.tinySize; font.weight: Font.DemiBold; font.letterSpacing: 0.6 }
            Rectangle {
                id: playRow
                readonly property color ink: Theme.focusText
                width: parent.width; height: 40; radius: 12
                color: Theme.focus
                Rectangle { anchors.fill: parent; radius: parent.radius; color: "#FFFFFF"; opacity: playHover.hovered ? 0.16 : 0; Behavior on opacity { NumberAnimation { duration: DesktopTokens.quickDuration } } }
                Shape {
                    x: 14; anchors.verticalCenter: parent.verticalCenter
                    width: 9; height: 11
                    layer.enabled: true; layer.samples: 4
                    ShapePath {
                        fillColor: playRow.ink; strokeColor: playRow.ink; strokeWidth: 1.5; joinStyle: ShapePath.RoundJoin
                        startX: 0.75; startY: 0.75
                        PathLine { x: 8.25; y: 5.5 }
                        PathLine { x: 0.75; y: 10.25 }
                        PathLine { x: 0.75; y: 0.75 }
                    }
                }
                Text { x: 34; anchors.verticalCenter: parent.verticalCenter; text: qsTr("Play"); color: playRow.ink; font.family: DesktopTokens.bodyFont; font.pixelSize: DesktopTokens.captionSize; font.weight: Font.Black }
                KeyboardGlyph { anchors.right: parent.right; anchors.rightMargin: 12; anchors.verticalCenter: parent.verticalCenter; shortcut: "Enter"; keySize: 20; ink: playRow.ink; opacity: 0.8; Accessible.name: qsTr("Enter") }
                HoverHandler { id: playHover; cursorShape: Qt.PointingHandCursor }
                TapHandler { onTapped: root.activateContext("play") }
            }
            Item { width: parent.width; height: 6 }
            Repeater {
                model: [
                    {label:qsTr("Details"), key:"Space", action:"details"},
                    {label:root.favoriteLabel(), key:"F", action:"favorite"},
                    {label:qsTr("Add to collection"), key:"›", action:"collection"},
                    {label:qsTr("Stream settings…"), key:"Ctrl ,", action:"settings"},
                    {label:root.hideLabel(), key:"", action:"hide"}
                ]
                delegate: ItemDelegate {
                    required property var modelData
                    width: 214; height: 32; padding: 8
                    highlighted: modelData.action === "collection" && root.collectionOpen
                    background: Rectangle { radius: 7; color: parent.hovered || parent.activeFocus || (modelData.action === "collection" && root.collectionOpen) ? "#14FFFFFF" : "transparent" }
                    contentItem: Item {
                        Text { anchors.left: parent.left; anchors.verticalCenter: parent.verticalCenter; text: modelData.label; color: DesktopTokens.textBody; font.family: DesktopTokens.bodyFont; font.pixelSize: DesktopTokens.captionSize }
                        KeyboardGlyph { visible: modelData.action !== "collection"; anchors.right: parent.right; anchors.verticalCenter: parent.verticalCenter; shortcut: modelData.key; keySize: 18; ink: DesktopTokens.textMuted }
                        Text { visible: modelData.action === "collection"; anchors.right: parent.right; anchors.verticalCenter: parent.verticalCenter; text: modelData.key; color: DesktopTokens.textFaint; font.family: DesktopTokens.monoFont; font.pixelSize: DesktopTokens.tinySize }
                    }
                    onClicked: {
                        if (modelData.action === "collection")
                            root.collectionOpen = !root.collectionOpen
                        else
                            root.activateContext(modelData.action)
                    }
                }
            }
        }
        opacity: contextMotion.progress
        scale: contextMotion.zoom
        transformOrigin: Item.TopLeft
    }
    Rectangle {
        x: Math.max(8, Math.min(root.width - width - 8, root.contextPoint.x + 238))
        y: Math.max(8, Math.min(root.height - height - 8, root.contextPoint.y + 104))
        width: 240
        height: Math.min(root.height - 16, 96 + Math.min(6, ShellStore.gameCollections.length) * 36 + (ShellStore.collectionError ? 52 : 0))
        radius: 12
        visible: collectionMotion.present
        enabled: root.contextGame !== null && root.collectionOpen
        z: 42
        color: DesktopTokens.shell
        border.width: 1; border.color: DesktopTokens.seam
        Column {
            x: 8; y: 8; width: parent.width - 16; spacing: 0
            Text { width: parent.width; height: 24; leftPadding: 8; text: qsTr("COLLECTIONS"); color: DesktopTokens.textFaint; verticalAlignment: Text.AlignVCenter; font.family: DesktopTokens.monoFont; font.pixelSize: DesktopTokens.tinySize; font.weight: Font.DemiBold; font.letterSpacing: 0.6 }
            ListView {
                width: parent.width
                height: Math.min(6, count) * 36
                clip: true
                model: ShellStore.gameCollections
                boundsBehavior: Flickable.StopAtBounds
                ScrollBar.vertical: ScrollBar { }
                delegate: ItemDelegate {
                    id: membershipButton
                    required property var modelData
                    width: ListView.view.width
                    height: 36
                    padding: 8
                    enabled: !ShellStore.collectionsBusy
                    Accessible.name: modelData.name
                    background: Rectangle { radius: 7; color: membershipButton.hovered || membershipButton.activeFocus ? DesktopTokens.raised : "transparent" }
                    contentItem: Text {
                        text: (ShellStore.isInCollection(root.contextGame, membershipButton.modelData.id) ? "✓  " : "+  ") + membershipButton.modelData.name
                        textFormat: Text.PlainText
                        elide: Text.ElideRight
                        verticalAlignment: Text.AlignVCenter
                        color: DesktopTokens.textBody
                        font.family: DesktopTokens.bodyFont
                        font.pixelSize: DesktopTokens.captionSize
                    }
                    onClicked: ShellStore.toggleCollectionGame(modelData.id, root.contextGame)
                }
            }
            ItemDelegate {
                id: newCollectionAction
                width: parent.width; height: 40; padding: 8
                enabled: !ShellStore.collectionsBusy
                background: Rectangle { radius: 7; color: newCollectionAction.hovered || newCollectionAction.activeFocus ? DesktopTokens.raised : "transparent" }
                contentItem: Text {
                    text: qsTr("New collection")
                    verticalAlignment: Text.AlignVCenter
                    color: DesktopTokens.textBody
                    font.family: DesktopTokens.bodyFont
                    font.pixelSize: DesktopTokens.captionSize
                }
                onClicked: root.editCollection("create", root.contextGame)
            }
            Text {
                width: parent.width
                height: visible ? 52 : 0
                visible: ShellStore.collectionError !== ""
                text: ShellStore.collectionError
                textFormat: Text.PlainText
                wrapMode: Text.WordWrap
                color: DesktopTokens.textMuted
                font.family: DesktopTokens.bodyFont
                font.pixelSize: 12
                }
        }
        opacity: collectionMotion.progress
        scale: collectionMotion.zoom
        transformOrigin: Item.TopLeft
    }
}
